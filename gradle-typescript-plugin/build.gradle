apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.ant:ant:1.9.2'
}

repositories {
    mavenCentral()
}

group = "at.irian.typescript"
version = "0.5"

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'gradle-typescript-plugin'
                description 'Gradle plugin for projects using the TypeScript language'
                url 'https://github.com/jzarnikov/gradle-typescript-plugin'

               scm {
                   url 'scm:git:git@github.com:jzarnikov/gradle-typescript-plugin.git'
                   connection 'scm:git:git@github.com:jzarnikov/gradle-typescript-plugin.git'
                   developerConnection 'scm:git:git@github.com:jzarnikov/gradle-typescript-plugin.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

                developers {
                    developer {
                        id 'jzarnikov'
                        name 'Jan Zarnikov'
                    }
                }
            }
            pom.withXml { root ->
                def children = root.asNode().children()
                def versionIndex = children.indexOf(children.find {it.name().localPart == 'artifactId'})
                children.add(versionIndex + 1, new Node(null, 'packaging', 'jar'))
            }
        }
    }
}

configure(install.repositories.mavenInstaller) {
    pom.project {
       name 'gradle-typescript-plugin'
       description 'Gradle plugin for projects using the TypeScript language'
       url 'https://github.com/jzarnikov/gradle-typescript-plugin'

       scm {
           url 'https://github.com/jzarnikov/gradle-typescript-plugin.git'
           connection 'scm:git:https://github.com/jzarnikov/gradle-typescript-plugin.git'
           developerConnection 'scm:git:https://github.com/jzarnikov/gradle-typescript-plugin.git'
       }

       licenses {
           license {
               name 'The Apache Software License, Version 2.0'
               url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
               distribution 'repo'
           }
       }

       developers {
           developer {
               id 'jzarnikov'
               name 'Jan Zarnikov'
           }
       }
    }

    // workaround to specify an explicit <packaging> in generated pom (as required by maven central)
    // see http://issues.gradle.org/browse/GRADLE-1285
    pom.withXml { root ->
        def children = root.asNode().children()
        def versionIndex = children.indexOf(children.find {it.name().localPart == 'artifactId'})
        children.add(versionIndex + 1, new Node(null, 'packaging', 'jar'))
    }
}